plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'com.github.triplet.play' version '2.1.0'
}

android {
    compileSdkVersion versions.compileSdk
    defaultConfig {
        applicationId "com.jacquessmuts.positivitea"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionName "0.1.1"
        versionCode getBuildVersion() as int
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        archivesBaseName = "positivitea-$versionName-$versionCode"
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("KEYSTORE") ?: "keystore.jks")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEYSTORE_ALIAS")
            keyPassword System.getenv("KEYSTORE_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "access"
    productFlavors {
        user {

        }
        moderator {

        }
    }

    lintOptions {
        warning 'InvalidPackage'
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

// Don't name this getVersionCode() or things will break
def getBuildVersion() {
    def version = 1
    def buildNumber = System.getenv("CIRCLE_BUILD_NUM")
    if (buildNumber != null) {
        version += buildNumber
    }
    println("VersionCode: " + version)
    return version
}

// Configuration for com.github.triplet.play deployment
play {
    track = 'alpha'
    serviceAccountCredentials = file(System.getenv("PRIVATE_KEY") ?: "private_key.json")
    resolutionStrategy = "ignore" //If the deployment fails due to an API error, don't fail the CI Build
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(path: ':api')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin.core"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versions.kotlin.coroutines"

    // Official Android
    implementation 'androidx.core:core:1.0.1'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.work:work-runtime-ktx:2.0.0"
    implementation "android.arch.lifecycle:extensions:$versions.android.lifecycle"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    kapt "android.arch.lifecycle:compiler:$versions.android.lifecycle"

    // Database
    implementation "androidx.room:room-runtime:$versions.android.room"
    implementation "androidx.room:room-rxjava2:$versions.android.room"
    implementation "androidx.room:room-coroutines:$versions.android.room"
    kapt "androidx.room:room-compiler:$versions.android.room"

    // Dependency Injection
    implementation "org.kodein.di:kodein-di-generic-jvm:$versions.kodein"
    implementation "org.kodein.di:kodein-di-framework-android-x:$versions.kodein"

    // RxJava
    implementation "io.reactivex.rxjava2:rxandroid:$versions.rx.android"
    implementation "io.reactivex.rxjava2:rxjava:$versions.rx.core"
    implementation "io.reactivex.rxjava2:rxkotlin:$versions.rx.kotlin"
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$versions.rx.binding"
    implementation "com.jakewharton.rxbinding2:rxbinding-design:$versions.rx.binding"
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.0'

    // Jake Wharton
    implementation deps.timber

    // Testing
    testImplementation "org.jetbrains.kotlin:kotlin-test:$versions.kotlin.core"
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.21.0'
    testImplementation 'androidx.test:core:1.1.0'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
}

apply plugin: 'com.google.gms.google-services'
